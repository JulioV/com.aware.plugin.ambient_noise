apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = version_readable
group = "com.awareframework"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
//        applicationId "com.aware.plugin.ambient_noise"
        minSdkVersion 10
        targetSdkVersion 25
        versionCode version_code
        versionName version_readable
    }

    signingConfigs {
        release {
            //set values in ~/.gradle/gradle.properties to sign with your own keystore and certificate
            storeFile file(project.hasProperty("KEYSTORE_FILE") ? KEYSTORE_FILE : "/")
            storePassword project.hasProperty("KEYSTORE_PASSWORD") ? KEYSTORE_PASSWORD : ""
            keyAlias project.hasProperty("KEY") ? KEY : ""
            keyPassword project.hasProperty("KEY_PASSWORD") ? KEY_PASSWORD : ""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

bintray {
    user = project.hasProperty("bintrayUsername") ? bintrayUsername : ""
    key = project.hasProperty("bintrayKey") ? bintrayKey : ""

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'com.awareframework'
        name = "com.aware.plugin.ambient_noise"
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/denzilferreira/com.aware.plugin.ambient_noise'
        issueTrackerUrl = 'https://github.com/denzilferreira/com.aware.plugin.ambient_noise/issues'
        labels = ['aar', 'android', 'framework', 'aware']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/denzilferreira/com.aware.plugin.ambient_noise.git'
        desc = "AWARE Plugin: Ambient Noise"
        userOrg = user
        githubRepo = "denzilferreira/com.aware.plugin.ambient_noise"

        version {
            name = "$version_readable"
            desc = bintray.pkg.desc
            released = new Date()
            vcsTag = "$version_readable"
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}

repositories {
    maven { url "https://jitpack.io" } //MPAndroidChart
    maven { url "http://dl.bintray.com/jfrog/bintray-tools" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${support_libs}"
    compile "com.awareframework:aware-core:${aware_libs}@aar"
//    compile 'com.github.denzilferreira:aware-client:development-SNAPSHOT'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    compile 'com.koushikdutta.ion:ion:2.1.6'
    compile 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.0.2'
    compile 'me.franfernandez:fftpack:0.1'
}

